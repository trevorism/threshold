buildscript {
    ext{
        creds = {
            username = findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://maven.pkg.github.com/trevorism/gradle-acceptance-plugin")
            credentials creds
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.5'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.3.1'
    }
}

plugins {
    id "groovy"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "3.7.8"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"

repositories {
    mavenCentral()
    maven {
        url uri("https://maven.pkg.github.com/trevorism/secure-utils")
        credentials creds
    }
}

dependencies {
    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.security:micronaut-security")
    implementation("io.micronaut:micronaut-validation")

    implementation("jakarta.annotation:jakarta.annotation-api")

    implementation("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.openapi:micronaut-openapi"){
        exclude module:"slf4j-nop"
    }

    runtimeOnly("ch.qos.logback:logback-classic")
    implementation 'org.slf4j:slf4j-simple:2.0.6'

    implementation 'com.trevorism:secure-utils:5.0.0'
    implementation 'com.trevorism:micronaut-utility-beans:0.1.0'
    implementation 'com.trevorism:datastore-client:3.1.0'

    acceptanceImplementation 'io.cucumber:cucumber-groovy:6.10.4'

}

application {
    mainClass.set("com.trevorism.Application")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.trevorism.*")
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
    groovyOptions.forkOptions.jvmArgs.add('-Dmicronaut.openapi.views.spec=rapidoc.enabled=false,swagger-ui.enabled=true,swagger-ui.theme=flattop')
}

appengine {
    stage.artifact = "${buildDir}/libs/${project.name}-all.jar"
    deploy {
        projectId = "trevorism-action"
        version = "0-7-0"
    }
}
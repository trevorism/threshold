buildscript {
    ext{
        creds = {
            username = findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
            password = findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://maven.pkg.github.com/trevorism/gradle-acceptance-plugin")
            credentials creds
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.8.0'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.7.2'
    }
}

plugins {
    id "groovy"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.3.8"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"

repositories {
    mavenCentral()
    maven {
        url uri("https://maven.pkg.github.com/trevorism/secure-utils")
        credentials creds
    }
}

dependencies {
    annotationProcessor("io.micronaut.validation:micronaut-validation-processor")

    implementation("io.micronaut.groovy:micronaut-runtime-groovy")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut.security:micronaut-security")
    runtimeOnly("org.yaml:snakeyaml")

    implementation("jakarta.annotation:jakarta.annotation-api")

    implementation("io.swagger.core.v3:swagger-annotations")
    compileOnly("io.micronaut.openapi:micronaut-openapi") {
        exclude module: "slf4j-nop"
    }

    runtimeOnly("ch.qos.logback:logback-classic")

    implementation 'com.trevorism:secure-utils:5.0.0'
    implementation ('com.trevorism:micronaut-utility-beans:1.3.0'){
        exclude module:"slf4j-nop"
        exclude module:"slf4j-simple"
    }
    implementation ('com.trevorism:datastore-client:3.2.0'){
        exclude module:"slf4j-nop"
    }

    testImplementation 'com.trevorism:test-result-events:0.3.0'
    acceptanceImplementation 'io.cucumber:cucumber-groovy:6.10.4'
}

application {
    mainClass.set("com.trevorism.Application")
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.trevorism.*")
    }
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
    groovyOptions.forkOptions.jvmArgs.add('-Dmicronaut.openapi.views.spec=rapidoc.enabled=false,swagger-ui.enabled=true,swagger-ui.theme=flattop')
}

appengine {
    stage.artifact = layout.buildDirectory.file("libs/${project.name}-all.jar")
    deploy {
        projectId = "trevorism-action"
        version = "0-8-0"
        stopPreviousVersion = true
        promote = true
    }
}

test {
    systemProperty("trevorism.test.event", findProperty("trevorism.test.event") == "enabled" ? project.name : null)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.1
            }
        }
    }
}

build.dependsOn jacocoTestCoverageVerification